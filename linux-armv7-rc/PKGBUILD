buildarch=4

_rcver=3.19.0
_rcrel=5
_rcnrel=armv7-x3

pkgbase=linux-armv7-rc
_srcname=linux-3.$((${_rcver##*.}-1))
_kernelname=${pkgbase#linux}
_desc="ARMv7 multi-platform"
pkgver=${_rcver}.rc${_rcrel}
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
makedepends=()
options=('!strip')
source=("${_rcver}-rc${_rcrel}-${_rcnrel}.zImage"
        "${_rcver}-rc${_rcrel}-${_rcnrel}-dtbs.tar.gz"
        "${_rcver}-rc${_rcrel}-${_rcnrel}-firmware.tar.gz"
        "${_rcver}-rc${_rcrel}-${_rcnrel}-modules.tar.gz"
        "config-${_rcver}-rc${_rcrel}-${_rcnrel}")
noextract=("${_rcver}-rc${_rcrel}-${_rcnrel}-dtbs.tar.gz"
           "${_rcver}-rc${_rcrel}-${_rcnrel}-firmware.tar.gz"
           "${_rcver}-rc${_rcrel}-${_rcnrel}-modules.tar.gz")
md5sums=('c2bf57d84365f9d3245e8e5e34020fe0'
         'ec2092a757d8b4971ffab8ef38cf1c61'
         'e8845288ee598d29f29f5d552197e01a'
         '8e9fa9dbcce175f1ea6ba1a86b6ee1f4'
         '76209736ddc0bce1293802c9f85159de')

_package() {
  pkgdesc="The Linux Kernel and modules - ${_desc}"
  depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio>=0.7')
  optdepends=('crda: to set the correct wireless channels of your country')
  provides=('kernel26' "linux=${pkgver}" 'aufs_friendly')
  conflicts=('linux')
  replaces=('linux-mvebu')
  install=${pkgname}.install

  cd "${srcdir}"

  KARCH=arm

  # get kernel version
  _kernver="${_rcver}-rc${_rcrel}-${_rcnrel}"
  _basekernel=${_kernver%%-*}
  _basekernel=${_basekernel%.*}

  mkdir -p "${pkgdir}"/{lib/modules,lib/firmware,boot/dtbs}
  tar -xf "${_rcver}-rc${_rcrel}-${_rcnrel}-modules.tar.gz" -C "${pkgdir}"
  tar -xf "${_rcver}-rc${_rcrel}-${_rcnrel}-firmware.tar.gz" -C "${pkgdir}"/lib/firmware
  tar -xf "${_rcver}-rc${_rcrel}-${_rcnrel}-dtbs.tar.gz" -C "${pkgdir}"/boot/dtbs
  cp "${_rcver}-rc${_rcrel}-${_rcnrel}.zImage" "${pkgdir}"/boot/zImage
  
  chown -R root:root "${pkgdir}"
  chmod -R g-w "${pkgdir}"
  

  # set correct depmod command for install
  sed \
    -e  "s/KERNEL_NAME=.*/KERNEL_NAME=${_kernelname}/g" \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
    -i "${startdir}/${pkgname}.install"

  # remove build and source links
  rm -f "${pkgdir}"/lib/modules/${_kernver}/{source,build}
  # remove the firmware
  rm -rf "${pkgdir}/lib/firmware"
  # gzip -9 all modules to save 100MB of space
  find "${pkgdir}" -name '*.ko' |xargs -P 2 -n 1 gzip -9
  # make room for external modules
  ln -s "../extramodules-${_basekernel}-${_kernelname:-ARCH}" "${pkgdir}/lib/modules/${_kernver}/extramodules"
  # add real version for building modules and running depmod from post_install/upgrade
  mkdir -p "${pkgdir}/lib/modules/extramodules-${_basekernel}-${_kernelname:-ARCH}"
  echo "${_kernver}" > "${pkgdir}/lib/modules/extramodules-${_basekernel}-${_kernelname:-ARCH}/version"

  # Now we call depmod...
  #depmod -b "$pkgdir" -F System.map "$_kernver"

  # move module tree /lib -> /usr/lib
  mkdir -p "${pkgdir}/usr"
  mv "$pkgdir/lib" "$pkgdir/usr"
}

pkgname=("${pkgbase}")
for _p in ${pkgname[@]}; do
  eval "package_${_p}() {
    _package${_p#${pkgbase}}
  }"
done
