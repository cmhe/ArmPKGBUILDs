# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable omap, exynos, freedreno for v7
#  - drop valgrind makdepend

buildarch=4

pkgname=libdrm-etna-git
pkgver=
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('armv7h')
license=('custom')
depends=('glibc' 'libpciaccess')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
provides=('libdrm')
conflicts=('libdrm')
url="http://dri.freedesktop.org/"
source=('libdrm::git+https://github.com/austriancoder/libdrm.git'
        COPYING)
sha256sums=(SKIP
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

pkgver() {
  cd "${srcdir}/libdrm"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/libdrm"

  [[ "$CARCH" == "armv7h" ]] && CONFIG="--enable-omap-experimental-api --enable-exynos-experimental-api --enable-freedreno-experimental-api --enable-etnaviv-experimental-api"

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install
  
  ./configure --prefix=/usr --enable-udev $CONFIG
  make
}

check() {
  cd "${srcdir}/libdrm"
  make -k check
}

package() {
  cd "${srcdir}/libdrm"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
